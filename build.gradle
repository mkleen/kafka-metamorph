buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.3.5'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.5'
}

allprojects {
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'net.researchgate.release'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
    }

    release {
        tagTemplate = '$name-$version'
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    bintray {
        dryRun = false // Whether to run this as dry-run, without deploying
        publish = false //If version should be auto published after an upload

        user = project.hasProperty('bintrayUser') ? bintrayUser : ''
        key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
        configurations = ['archives']
        pkg {
            name = 'kafka-metamorph'
            repo = 'maven'
            userOrg = 'researchgate'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/researchgate/kafka-metamorph.git'
            issueTrackerUrl = 'https://github.com/researchgate/kafka-metamorph/issues'
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }
}

project(':metamorph-kafka-08') {
    dependencies {
        compile project(':metamorph-common')
        compile 'org.apache.kafka:kafka_2.10:0.8.2.2'
    }
}

